plugins {
    id "net.ltgt.errorprone" version "0.0.13"
}

new RoboJavaModulePlugin(
        deploy: true
).apply(project)

task prefetchSdks() {
    def properties = new Properties()
    AndroidSdk.ALL_SDKS.each { androidSdk ->
        def config = configurations.create("sdk${androidSdk.apiLevel}")
        dependencies.add("sdk${androidSdk.apiLevel}", androidSdk.coordinates)
        properties.setProperty("${androidSdk.apiLevel}", config.files.first().getAbsolutePath())
    }
    def out = new File(project.sourceSets['main'].output.resourcesDir, 'sdks.properties')
    def writer = new FileWriter(out)
    properties.store(writer, "")
    writer.close()
}

tasks['compileJava'].dependsOn(prefetchSdks)

// Disable annotation processor for tests
compileTestJava {
    options.compilerArgs.add("-proc:none")
}

dependencies {
    // Project dependencies
    compile project(":annotations")

    // Compile dependencies
    compile "com.google.guava:guava:20.0"
    compileOnly "com.google.code.findbugs:jsr305:3.0.1"
    compile "com.google.code.gson:gson:2.8.0"
    compile 'ch.raffael.pegdown-doclet:pegdown-doclet:1.3'

    compile files(org.gradle.internal.jvm.Jvm.current().getToolsJar())

    // Testing dependencies
    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-core:2.5.4"
    testCompile "com.google.testing.compile:compile-testing:0.12"
    testCompile "com.google.truth:truth:0.36"
}
